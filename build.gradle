buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.0"
    id "org.jetbrains.kotlin.kapt" version "1.3.0"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

apply plugin: "net.minecraftforge.gradle.forge"

group "de.randombyte"
version "1.0.0"

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = "snapshot_20171003"
}

repositories {
    jcenter()
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
    maven { url "https://repo.spongepowered.org/maven/" }
    maven { url "https://jitpack.io" }
    maven { url "http://repo.bstats.org/content/repositories/releases" }
}

configurations {
    compile.extendsFrom shadow
    compile.extendsFrom kapt
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.0"
    kapt "org.spongepowered:spongeapi:7.0.0"
    shadow("com.github.randombyte-developer.kosp:kosp:v2.2.3") { transitive = false }
    compile files("libs/Pixelmon-1.12.2-6.3.4-server.jar")
    shadow "org.bstats:bstats-sponge:1.2"
}

jar.enabled = false
shadowJar {
    configurations = [project.configurations.shadow]

    relocate "kotlin", "de.randombyte.pokebattleclauses.shaded.kotlin"
    relocate "de.randombyte.kosp", "de.randombyte.pokebattleclauses.shaded.kosp"
    relocate "org.bstats", "de.randombyte.pokebattleclauses.shaded.bstats"

    classifier = null // Remove "-all" suffix from output file name
}
build.dependsOn shadowJar
reobfJar.dependsOn shadowJar

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}